import com.vanniktech.maven.publish.*

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'jacoco'
    id 'com.vanniktech.maven.publish' version '0.32.0' // publish to Maven Central
    id 'com.github.ben-manes.versions' version '0.52.0' // check for out-of-date dependencies (run 'dependencyUpdates' manually)
    id 'com.github.spotbugs' version '6.1.11' // spotbugs code analysis
    id 'org.sonatype.gradle.plugins.scan' version '3.1.1' // scan for vulnerabilities
    id 'org.sonarqube' version '6.1.0.5360' // sonarQube analysis
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'Cancer-related synthetic data generator.'

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    api 'com.imsweb:layout:6.3'
    api 'com.imsweb:naaccr-xml:11.3'

    implementation 'com.imsweb:seerutils:5.7'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'commons-io:commons-io:2.19.0'

    testImplementation 'com.imsweb:validation:4.3'
    testImplementation 'com.imsweb:staging-client-java:11.4.0'
    testImplementation 'com.imsweb:staging-client-java-cs:11.4.0'
    testImplementation 'com.imsweb:staging-client-java-eod-public:11.4.0'
    testImplementation 'com.imsweb:staging-client-java-tnm:11.4.0'
    testImplementation 'junit:junit:4.13.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
    options.addBooleanOption('html5', true)
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.datagenerator',
                'Main-Class': 'com.imsweb.datagenerator.gui.StandaloneNaaccrDataGenerator')
    }
    from('VERSION') {
        rename 'VERSION', 'DATA-GENERATOR-VERSION'
    }
    from('LICENSE') {
        rename 'LICENSE', 'DATA-GENERATOR-LICENSE'
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "imsweb_data-generator"
        property "sonar.organization", "imsweb"
        property "sonar.host.url", "https://sonarcloud.io"
        property 'sonar.exclusions', '**/lab/*'
        property 'sonar.coverage.exclusions', '**/lab/*'
    }
}

// Nexus vulnerability scan (see https://github.com/sonatype-nexus-community/scan-gradle-plugin)
ossIndexAudit {
    outputFormat = 'DEPENDENCY_GRAPH'
    printBanner = false
}
check.dependsOn 'ossIndexAudit'

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': project(':').file('config/checkstyle/suppressions.xml')]
}

// spotbugs plugin settings
spotbugs {
    ignoreFailures = true
    excludeFilter.set(project(':').file("config/spotbugs/spotbugs-exclude.xml"))
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}
test.finalizedBy jacocoTestReport

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return (!stableKeyword && !(version ==~ regex)) || version == '021-11'
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

// needed to deploy to Maven Central Portal
mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)
    signAllPublications()

    pom {
        name = 'Synthetic Data Generator'
        description = 'This Java library can be used to create synthetic data files.'
        url = 'https://github.com/imsweb/data-generator'
        inceptionYear = '2016'

        licenses {
            license {
                name = 'A modified BSD License (BSD)'
                url = 'https://github.com/imsweb/data-generator/blob/master/LICENSE'
                distribution = 'repo'
            }
        }

        developers {
            developer {
                id = 'depryf'
                name = 'Fabian Depry'
                email = 'depryf@imsweb.com'
            }
        }

        scm {
            url = 'https://github.com/imsweb/data-generator'
            connection = 'scm:https://github.com/imsweb/data-generator.git'
            developerConnection = 'scm:git@github.com:imsweb/data-generator.git'
        }
    }
}

// Gradle wrapper, this allows to build the project without having to install Gradle...
wrapper {
    gradleVersion = '8.14'
    distributionType = Wrapper.DistributionType.ALL
}
