import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'checkstyle'
    id 'findbugs'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.8.0'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'Cancer-related synthetic data generator.'

println "Starting build using ${Jvm.current()}"

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'com.imsweb:layout:1.6'
    compile 'com.imsweb:algorithms:1.9.1'
    compile 'com.imsweb:staging-client-java:2.9'
    compile 'commons-io:commons-io:2.5'
    testCompile 'junit:junit:4.12'
}

// enforce UTF-8 for all compilation tasks
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// display the compilation warnings
compileJava.options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
compileTestJava.options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Main-Class': 'com.imsweb.datagenerator.gui.StandaloneNaaccrDataGenerator')
    }
    from ('VERSION') {
        rename 'VERSION', 'DATA-GENERATOR-VERSION'
    }
    from ('LICENSE') {
        rename 'LICENSE', 'DATA-GENERATOR-LICENSE'
    }
}

// this sucks, but they made the Javadoc way too stritct in Java 8 and it's not worth my time fixing it!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}


// we don't want these files in the "all" jar, it's confusing
shadowJar {
    exclude('XMLPULL_1_1_3_1_VERSION')
    exclude('XPP3_1.1.4c_MIN_VERSION')
    from('VERSION') {
        rename 'VERSION', 'DATA-GENERATOR-VERSION'
    }
    from('LICENSE') {
        rename 'LICENSE', 'DATA-GENERATOR-LICENSE'
    }
}

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = file('config/checkstyle/checkstyle.xml')
}

// findbugs plugin settings
findbugs {
    ignoreFailures = true
    effort = 'max'
    excludeFilter = file('config/findbugs/findbugs-exclude.xml')
}

// needed to deploy to Maven Central
modifyPom {
    project {
        name 'Synthetic data generator.'
        description 'This Java library can be used to create synthetic data files.'
        url 'https://github.com/imsweb/data-generator'
        inceptionYear '2016'

        scm {
            url 'https://github.com/imsweb/data-generator'
            connection 'scm:https://github.com/imsweb/data-generator.git'
            developerConnection 'scm:git@github.com:imsweb/data-generator.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/data-generator/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
            developer {
                id 'nakamurae'
                name 'Nakamur Eric'
                email 'nakamurae@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    closeRepository.enabled = false

// Gradle wrapper, this allows to build the project without having to install Gradle!
task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}


