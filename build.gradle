plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.14'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'org.sonatype.gradle.plugins.scan' version '2.2.2'
    id "com.github.ben-manes.versions" version '0.47.0'
    id "org.sonarqube" version "3.5.0.2730"
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'Cancer-related synthetic data generator.'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    api 'com.imsweb:layout:4.9'
    api 'com.imsweb:naaccr-xml:8.10'

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.13.0'

    testImplementation 'com.imsweb:seerutils:5.6'
    testImplementation 'com.imsweb:validation:2.26'
    testImplementation 'com.imsweb:staging-client-java:6.2.0'
    testImplementation 'com.imsweb:staging-algorithm-cs:02.05.50.9'
    testImplementation 'com.imsweb:staging-algorithm-tnm:1.9.8'
    testImplementation 'com.imsweb:staging-algorithm-eod-public:2.1.1'
    testImplementation 'junit:junit:4.13.2'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// generate javadoc and sources (required by Nexus)
java {
    withJavadocJar()
    withSourcesJar()
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.datagenerator',
                'Main-Class': 'com.imsweb.datagenerator.gui.StandaloneNaaccrDataGenerator')
    }
    from('VERSION') {
        rename 'VERSION', 'DATA-GENERATOR-VERSION'
    }
    from('LICENSE') {
        rename 'LICENSE', 'DATA-GENERATOR-LICENSE'
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "imsweb_data-generator"
        property "sonar.organization", "imsweb"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

// Nexus vulnerability scan (https://github.com/sonatype-nexus-community/scan-gradle-plugin)
ossIndexAudit {
    dependencyGraph = true
    printBanner = false
}
check.dependsOn 'ossIndexAudit'

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': project(':').file('config/checkstyle/suppressions.xml')]
}

// spotbugs plugin settings
spotbugs {
    ignoreFailures = true
    excludeFilter = project(':').file("config/spotbugs/spotbugs-exclude.xml")
}

// needed to deploy to Maven Central
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'data-generator'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Synthetic Data Generator'
                description = 'This Java library can be used to create synthetic data files.'
                url = 'https://github.com/imsweb/data-generator'
                inceptionYear = '2016'

                licenses {
                    license {
                        name = 'A modified BSD License (BSD)'
                        url = 'https://github.com/imsweb/data-generator/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'depryf'
                        name = 'Fabian Depry'
                        email = 'depryf@imsweb.com'
                    }
                }

                scm {
                    url = 'https://github.com/imsweb/data-generator'
                    connection = 'scm:https://github.com/imsweb/data-generator.git'
                    developerConnection = 'scm:git@github.com:imsweb/data-generator.git'
                }
            }
        }
    }
}

// setup JAR signing
signing {
    def signingKey = project.findProperty('signing.armored.key') ?: ''
    def signingPassword = project.findProperty('signing.armored.password') ?: ''

    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}

// needed to release on maven central
nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = '63e5ddd3ab0d16'

            username = project.findProperty("nexusUsername")
            password = project.findProperty("nexusPassword")
        }
    }

    clientTimeout = java.time.Duration.ofSeconds(300)
    connectTimeout = java.time.Duration.ofSeconds(60)

    transitionCheckOptions {
        maxRetries.set(50)
        delayBetween.set(java.time.Duration.ofMillis(5000))
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT')) {
    gradle.startParameter.excludedTaskNames += 'signMavenJavaPublication'
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseSonatypeStagingRepository'
}

// Gradle wrapper, this allows to build the project without having to install Gradle...
wrapper {
    gradleVersion = '7.6'
    distributionType = Wrapper.DistributionType.ALL
}
